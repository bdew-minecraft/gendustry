buildscript {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "gradle plugins"
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
    }
}

apply plugin: 'scala'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

if (project.hasProperty('forgeOverride')) {
    config.forge.version = forgeOverride
}

if (project.hasProperty('buildnum')) {
    ext.simpleVersion = "${config.gendustry.version}.${project.buildnum}"
} else {
    ext.simpleVersion = "${config.gendustry.version}-DEV"
}

version = simpleVersion + '-mc' + config.minecraft.version
ext.myApiVersion = config.gendustry.api.version

group = "net.bdew"
archivesBaseName = "gendustry"

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
    mappings = "${config.minecraft.mappings}"
    makeObfSourceJar = false
    replace("BDLIB_VER", config.bdlib.version)
    replace("GENDUSTRY_VER", simpleVersion.toString())
    replace("GENDUSTRY_APIVER", myApiVersion)
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "bdew"
        url = "https://jenkins.bdew.net/maven"
    }
    maven {
        name "JEI"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name "HWYLA"
        url "http://tehnut.info/maven"
    }
    maven {
        name = "tesla"
        url = "http://maven.epoxide.xyz"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    ivy {
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision].[ext]"
    }
}

dependencies {
    compile "net.bdew:bdlib:${config.bdlib.version}-mc${config.minecraft.version}:dev"
//    deobfCompile "com.mod-buildcraft:buildcraft:${config.buildcraft.version}"
    compile "net.industrial-craft:industrialcraft-2:${config.ic2.version}:api"
    compile "net.sengir.forestry:forestry_${config.minecraft.version}:${config.forestry.version}:api"

    deobfCompile "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${config.hwyla.version}"

    deobfCompile "cofh:RedstoneFlux:${config.minecraft.version}-${config.rfapi.version}:universal"
    compile "net.darkhax.tesla:Tesla-${config.minecraft.version}:${config.tesla.version}"
}


import org.apache.tools.ant.filters.ReplaceTokens

sourceSets {
    main {
        scala {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}

processResources {
    inputs.property "tokens", minecraft.replacements
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        filter(ReplaceTokens, tokens: minecraft.replacements)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include 'net/bdew/gendustry/api/**'
    }
    
    from(file('api-changelog.md'))

    duplicatesStrategy "exclude"

    extension = 'jar'
    classifier = 'api'
}

task sourceJarReal(type: Jar) {
    classifier "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    exclude "**/*.psd"
    classifier "dev"
    duplicatesStrategy "exclude"
}


jar {
    exclude "**/*.psd"
}

afterEvaluate { project ->
    // Fudge the inputs of api/source jars so we get the version after replacements
    tasks.getByPath(":sourceJarReal").from(tasks.getByPath(":sourceMainScala").outputs.files)
    tasks.getByPath(":apiJar").from(tasks.getByPath(":sourceMainScala").outputs.files, {
        include 'net/bdew/gendustry/api/**'
    })
}

artifacts {
    archives sourceJar
    archives deobfJar
    archives apiJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifact deobfJar
            artifact sourceJarReal
            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file://var/www/maven"
        }
    }
}

curseforge {
    apiKey = project.hasProperty("curseForgeApiKey") ? project.curseForgeApiKey : ""
    project {
        id = config.curseforge.id

        releaseType = "alpha"
        changelog = project.hasProperty("changelog") ? project.changelog : "No changelog available"

        addGameVersion config.minecraft.version

        mainArtifact(jar) {
            displayName = "Gendustry ${simpleVersion} (MC ${config.minecraft.version})"
        }

        addArtifact(deobfJar) {
            displayName = "Gendustry ${simpleVersion} Deobfuscated (MC ${config.minecraft.version})"
        }

        addArtifact(sourceJarReal) {
            displayName = "Gendustry ${simpleVersion} Source (MC ${config.minecraft.version})"
        }

        addArtifact(apiJar) {
            displayName = "Gendustry ${simpleVersion} API (MC ${config.minecraft.version})"
        }

        relations {
            requiredLibrary 'bdlib'
            requiredLibrary 'forestry'
            optionalLibrary 'waila'
            optionalLibrary 'buildcraft'
            optionalLibrary 'cofhcore'
            optionalLibrary 'just-enough-items-jei'
            optionalLibrary 'tesla'
        }
    }
}