buildscript {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'scala'
apply plugin: 'forge'

version = "1.4.4"
ext.bdlibver = "1.6.0.34"
ext.versuffix = "mc1710"
ext.apiVer = version

if (project.hasProperty('buildnum')) {
    project.version = project.version + '.' + project.buildnum
} else {
    project.version = project.version + '-DEV'
}

group = "net.bdew"
archivesBaseName = "gendustry-fu"

minecraft {
    version = "1.7.10-10.13.2.1231"
    replace("BDLIB_VER", project.bdlibver)
    replace("GENDUSTRY_VER", project.version)
    replace("GENDUSTRY_APIVER", project.apiVer)
}

repositories {
    mavenCentral()
    maven {
        name = "bdew"
        url = "http://jenkins.bdew.net/maven"
    }
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven"
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name "waila"
        url "http://mobiusstrip.eu/maven"
    }
}

dependencies {
    compile "net.bdew:bdlib:" + bdlibver
    compile "codechicken:NotEnoughItems:1.7.10-1.0.3.74:dev"
    compile "codechicken:CodeChickenLib:1.7.10-1.1.1.110:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.4.29:dev"
    compile "com.mod-buildcraft:buildcraft:6.2.6:dev"
    compile "net.industrial-craft:industrialcraft-2:2.2.650-experimental:api"
    compile "net.sengir.forestry:forestry_1.7.10:3.3.0.415-unstable:api"
    compile "mcp.mobius.waila:Waila:1.5.6a_1.7.10"
    compile "net.bdew.thirdparty:CoFHLib:R1.0.1:api"
}

import org.apache.tools.ant.filters.ReplaceTokens

sourceSets {
    main {
        scala {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
        output.resourcesDir = output.classesDir // Hack for IDEA
    }
}

processResources {
    inputs.property "tokens", minecraft.replacements
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        filter(ReplaceTokens, tokens: minecraft.replacements)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include 'net/bdew/gendustry/api/**'
    }

    extension = 'jar'
    classifier = 'api'
}

task sourceJar(type: Jar) {
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    exclude "**/*.psd"
}


jar {
    exclude "**/*.psd"
    appendix = project.versuffix
}

afterEvaluate { project ->
    // Fudge the inputs of api/source jars so we get the version after replacements
    tasks.getByPath(":sourceJar").from(tasks.getByPath(":sourceMainScala").outputs.files)
    tasks.getByPath(":apiJar").from(tasks.getByPath(":sourceMainScala").outputs.files, {
        include 'net/bdew/gendustry/api/**'
    })
}

artifacts {
    archives sourceJar
    archives deobfJar
    archives apiJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifact deobfJar

            artifact sourceJar {
                classifier "sources"
            }

            artifact apiJar
        }
    }
    repositories {
        maven {
            url "file://var/www/maven"
        }
    }
}